plugins {
    id 'com.palantir.git-version' version '0.15.0'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'idea'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.sonarqube' version '3.4.0.2513'
    id 'com.diffplug.spotless' version '6.12.0'
    id 'jacoco'
}

group = 'engineering.everest.starterkit'

def gitTagVersion = versionDetails()
version = gitTagVersion.commitDistance == 0
        ? gitTagVersion.lastTag
        : "${gitTagVersion.lastTag}+${gitTagVersion.commitDistance}-SNAPSHOT"

ext {
    storageVersion = '1.1.0'

    lombokVersion = '1.18.24'
    metadataExtractorVersion = '2.18.0'
    springBootVersion = '3.0.2'
    thumbnailatorVersion = '0.4.19'

    junitVersion = '5.9.2'
    mockitoVersion = '5.1.1'
    postgresDriverVersion = '42.5.1'
    zonkyEmbeddedDbVersion = '2.2.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    api "engineering.everest.starterkit:storage:${storageVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "net.coobird:thumbnailator:${thumbnailatorVersion}"
    implementation "com.drewnoakes:metadata-extractor:${metadataExtractorVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'org.springframework:spring-test'
    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.postgresql:postgresql:${postgresDriverVersion}"
    testImplementation "io.zonky.test:embedded-database-spring-test:${zonkyEmbeddedDbVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    java {
        withJavadocJar()
        withSourcesJar()
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
    java {
        eclipse().configFile("$rootDir/eclipse-formatter-config.xml")
        indentWithSpaces(4)
        trimTrailingWhitespace()
        removeUnusedImports()
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

test {
    useJUnitPlatform()
}

apply from: 'publishing.gradle'
apply from: 'sonar.gradle'
