plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'io.freefair.lombok' version '5.1.1'
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id "org.sonarqube" version "3.1.1"
}

apply plugin: 'io.freefair.lombok'

group = 'engineering.everest.starterkit'
sourceCompatibility = '1.11'
version = gitVersion()

repositories {
    mavenLocal()
    jcenter()
    maven {
        url  "https://dl.bintray.com/everestengineering/starterkit"
    }
}

test {
	useJUnitPlatform()
}

lombok {
    version = '1.18.16'
    generateLombokConfig.enabled = false
}

ext {
    storageVersion = '0.9.2'

    metadataExtractorVersion = '2.15.0'
    springBootVersion = '2.4.0'
    thumbnailatorVersion = '0.4.13'

    junitVersion = '5.7.0'
    mockitoVersion = '3.5.15'
    h2Version = '1.4.200'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    api "engineering.everest.starterkit:storage:${storageVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "net.coobird:thumbnailator:${thumbnailatorVersion}"
    implementation "com.drewnoakes:metadata-extractor:${metadataExtractorVersion}"

    testImplementation 'org.springframework:spring-test'
    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testRuntimeOnly "com.h2database:h2:${h2Version}"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    pkg {
        repo = 'starterkit'
        name = rootProject.name
        userOrg = 'everestengineering'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/everestengineering/starterkit-media'
    }
    publish = true
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "everest-engineering_lhotse-media"
        property "sonar.organization", "everestengineering"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}